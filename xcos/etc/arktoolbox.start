// Copyright (C) 2011 - DIGITEO

// This file is released under the 3-clause BSD license. See COPYING-BSD.

function arktoolboxlib = startModule()

  mprintf("Start ARKToolbox\n");

  if isdef("arktoolboxlib") then
    warning("ARKToolbox is already loaded");
    return;
  end

// check minimal version (xcosPal required)
// =============================================================================
  if ~isdef('xcosPal') then
    // and xcos features required
    error(gettext('Scilab 5.3.2 or more is required.'));
  end
// =============================================================================  
// force to load some libraries (dependancies)
  loadScicos();
// =============================================================================
  etc_tlbx  = get_absolute_file_path("arktoolbox.start");
  etc_tlbx  = getshortpathname(etc_tlbx);
  root_tlbx = strncpy( etc_tlbx, length(etc_tlbx)-length("\etc\") );

// Load functions library
// =============================================================================
  mprintf("\tLoad macros\n");
  pathmacros = pathconvert( root_tlbx ) + "macros" + filesep();
  arktoolboxlib = lib(pathmacros);

// Add blocks to the Xcos palette
// =============================================================================
  mprintf("\tLoad palette\n");
  pal = xcosPal("ARKToolbox");

  exec(pathconvert(root_tlbx+"/macros/load_defs.sce", %f));
  for i=1:size(blocks, "*")
    h5  = ls(root_tlbx + "/images/h5/"  + blocks(i) + "." + ["sod" "h5"]);
    gif = ls(root_tlbx + "/images/gif/" + blocks(i) + "." + ["png" "jpg" "gif"]);
    svg = ls(root_tlbx + "/images/svg/" + blocks(i) + "." + ["png" "jpg" "gif" "svg"]);

    style = struct();
    style.shape = "label";
    style.verticalLabelPosition = "middle";
    style.perimeter = "rectanglePerimeter";
    style.strokeColor = "black"; 
    style.fontColor = "black";
    style.noLabel = 0;
    style.spacing = 13;
    style.fillColor = "#cdcdcd";
    style.gradientColor = "white";
    style.rounded = 1;
    style.displayedLabel = blocks(i);
    //style.image = "file://" + svg(1);
    pal = xcosPalAddBlock(pal, h5(1), gif(1), style);

  end

  exec(pathconvert(root_tlbx+"/macros/unload_defs.sce", %f));

  if ~xcosPalAdd(pal) then
    error(msprintf(gettext("%s: Unable to export %s.\n"), "arktoolbox.start", "pal"));
  end

// Load simulation functions
// =============================================================================
  mprintf("\tLoad simulations functions\n");
  verboseMode = ilib_verbose();
  ilib_verbose(0);
  exec(pathconvert(root_tlbx+"/src/cpp/loader.sce", %f));
  ilib_verbose(verboseMode);

// load gateways
// =============================================================================
  mprintf("\tLoad gateways\n");
  verboseMode = ilib_verbose();
  ilib_verbose(0);
  exec(pathconvert(root_tlbx+"/sci_gateway/loader_gateway.sce", %f));
  ilib_verbose(verboseMode);

// Load and add help chapter
// =============================================================================
  if or(getscilabmode() == ["NW";"STD"]) then
    mprintf("\tLoad help\n");
    path_addchapter = pathconvert(root_tlbx+"/jar");
    if ( isdir(path_addchapter) <> [] ) then
        add_help_chapter("ARKToolbox", path_addchapter, %F);
    end
  end

// Load demos
// =============================================================================
  if or(getscilabmode() == ["NW";"STD"]) then
    mprintf("\tLoad demos\n");
    pathdemos = pathconvert(root_tlbx+"/demos/arktoolbox.dem.gateway.sce", %F, %T);
    add_demo("ARKToolbox", pathdemos);
  end

endfunction

if with_module('xcos') then
  arktoolboxlib = startModule();
  clear startModule; // remove startModule on stack
end
